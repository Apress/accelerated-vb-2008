Imports System

Public Structure Complex
    Private Real As Double
    Private Imaginary As Double

    Public Sub New(ByVal real As Double, ByVal imaginary As Double)
        Me.Real = real
        Me.Imaginary = imaginary
    End Sub

    Public Shared Function Add _
        (ByVal lhs As Complex, ByVal rhs As Complex) As Complex

        Return New Complex(lhs.Real + rhs.Real, lhs.Imaginary + rhs.Imaginary)
    End Function

    Public Shared Function Add _
        (ByVal lhs As Complex, ByVal rhs As Double) As Complex

        Return New Complex(rhs + lhs.Real, lhs.Imaginary)
    End Function

    Public Overrides Function ToString() As String
        Return System.String.Format("({0}, {1})", Real, Imaginary)
    End Function

    Public Shared Operator +(ByVal lhs As Complex, ByVal rhs As Complex) As Complex
        Return Add(lhs, rhs)
    End Operator

    Public Shared Operator +(ByVal lhs As Double, ByVal rhs As Complex) As Complex
        Return Add(rhs, lhs)
    End Operator

    Public Shared Operator +(ByVal lhs As Complex, ByVal rhs As Double) As Complex
        Return Add(lhs, rhs)
    End Operator
End Structure

Public Class EntryPoint
    Shared Sub Main()
        Dim cpx1 As Complex = New Complex(1.0, 3.0)
        Dim cpx2 As Complex = New Complex(1.0, 2.0)

        Dim cpx3 As Complex = cpx1 + cpx2
        Dim cpx4 As Complex = 20.0 + cpx1
        Dim cpx5 As Complex = cpx1 + 25.0

        Console.WriteLine("cpx1 == {0}", cpx1)
        Console.WriteLine("cpx2 == {0}", cpx2)
        Console.WriteLine("cpx3 == {0}", cpx3)
        Console.WriteLine("cpx4 == {0}", cpx4)
        Console.WriteLine("cpx5 == {0}", cpx5)
    End Sub
End Class

------------------------------------------------------------

